apply plugin: 'android-library'

buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:0.11.1'
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  compile 'com.android.support:support-v4:19.1.+'
  compile 'com.google.android.gms:play-services:4.4.52'
}

android {
  compileSdkVersion 19
  buildToolsVersion "19.1.0"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 19
    versionCode 11
    versionName '3.3.4'
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
    return; // Skip debug builds.
  }
  def task = project.tasks.create "${name}Jar", Jar
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  task.version android.defaultConfig.versionName
  artifacts.add('archives', task);
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    version = android.defaultConfig.versionName
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
